def sonatypeUsername = System.env['SONATYPE_USERNAME']
def sonatypePassword = System.env['SONATYPE_PASSWORD']

allprojects {
    ext {
        minSdkVersion = 16
        compileSdkVersion = 27
        targetSdkVersion = 27
        versionCode = 1
        kotlinVersion = kotlin_version
        versionName = '0.1.0-SNAPSHOT'
        buildToolsVersion = '27.0.3'
    }
}

buildscript {
    ext.kotlin_version = '1.2.51'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

allprojects {
    repositories {
        flatDir {
            dirs "${rootDir}/libs"
        }
        google()
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/releases/'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    apply plugin: 'maven'

    uploadArchives {
        repositories {
            mavenDeployer {
                if (sonatypeUsername == null) {
                    repository(url: 'file:///tmp/maven')
                } else {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    }
                    snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    }
                }

                pom.project {
                    groupId 'com.braintreepayments.api'
                    packaging 'jar'
                    url 'https://github.com/braintree/braintree-android-samsung-pay'

                    scm {
                        url 'scm:git@github.com:braintree/braintree_android.git'
                        connection 'scm:git@github.com:braintree/braintree_android.git'
                        developerConnection 'scm:git@github.com:braintree/braintree_android.git'
                    }

                    developers {
                        developer {
                            id 'devs'
                            name 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }

    apply plugin: 'signing'
    signing {
        required {
            sonatypeUsername != null && !version.endsWith("SNAPSHOT")
        }
        sign configurations.archives
    }

    ext {
        minSdkVersion = 16
        compileSdkVersion = 27
        targetSdkVersion = 27
        versionCode = 1
        kotlin_version = '1.2.51'
        versionName = '0.1.0-SNAPSHOT'
        buildToolsVersion = '27.0.3'
    }
}
